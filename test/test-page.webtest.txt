This is to test all core commands with test-page.html

START
  open browser
  go to http://localhost:8080/test/test-page.html
  set window position 0 0
  set window size 100 600
  see "click here to change background color"

CLICK
  click
  click "body div#change-color"

HIDDEN ELEMENT
  click #show-hidden
  verify element #hidden is visible
  click #change-color
  verify element #change-color style background-color is "rgba(255, 255, 0, 1)"
  verify element #hidden text matches "^Hidden\ Element"

CLICK LINK
  click link "Click Here To Change Url"

ENTER TEXT INTO
  enter text 'Hello World' into "Enter text into here"
  press "A" into input#my-input
  set speed 1 second

ENABLED/DISABLED
  click link "Click Here To Change Url"
  verify element #submit is disabled
  set speed 1 millisecond
  click #enable
  verify element #submit is enabled

RADIO BUTTON
  click #radio1
  verify element #radio2 is not selected
  verify element #hidden is not visible

VERIFY COMMANDS
  verify title is "Test Form"
  verify title matches "Form"
  verify url matches "^http:\/\/"
  verify text "click here to change something" not present
  verify text "Click Here To Change Url" present

INPUT RANGE
  set element #range value as 11
  verify element #range value is 11

CHECKBOX

SELECT TAG
  select "C" from #select
  verify element "#select option:nth-child(3)" selected

TEXTAREA
  click textarea
  clear textarea
  verify element textarea value is ""

SUBMIT
  click #enable
  submit

CLASS NAMES
  verify element #class-names with class test1
  verify element #class-names with class test2
  verify element #class-names without class test3

PRESENCE
  verify element #presence present
  verify element #presence-not not present

DRAG-AND-DROP
  drag element 'drag1' 10 pixel up, then drop

ALERT/CONFIRM
  run script "alert(12345)"
  verify alert "12345" and click ok
  run script "confirm(12345)"
  verify alert "12345" and click cancel

SAVE VARIABLE
  run script "var today=new Date(); return today" and save to "today"
  print variable "today"
  verify script "today.getYear() === (new Date()).getYear()"

  enter text 'We can save variable' into "input#my-input"
  save value of element "input#my-input" to 'myInputVariable'
  print variable "myInputVariable"
  verify script "myInputVariable === 'We can save variable'"

END
  wait for page load
  close browser
