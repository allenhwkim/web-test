This is to test all core commands with test-page.html

START
  open browser
  go to http://localhost:8080/test/test-page.html
  set window position 0 0
  set window size 800 600

CLICK
  click #blocker-btn1
  click #blocker-btn2
  see "click here to change background color"
  click
  click "body div#change-color"

HIDDEN ELEMENT
  find element #hidden
  run script "document.querySelector('#hidden').innerHTML='My Hidden Element'"
  click #show-hidden
  verify element #hidden is visible
  click #change-color
  verify element #change-color style background-color is "rgba(255, 255, 0, 1)"
  verify element #hidden text matches "Hidden\ Element"

CLICK LINK
  click #blocker-btn3
  click link "Click Here To Change Url"

ENTER TEXT INTO
  enter text 'Hello World' into "Enter text into here"
  press "A" into input#my-input

SET/RESET SPEED
  set speed 1 second
  reset speed
  set speed 2 second
  reset speed

ENABLED/DISABLED
  click link "Click Here To Change Url"
  verify element #submit is disabled
  click #enable
  verify element #submit is enabled

RADIO BUTTON
  click #radio1
  verify element #radio2 is not selected
  verify element #hidden is not visible

VERIFY COMMANDS
  verify title is "Test Form"
  verify title matches "Form"
  verify url matches "^http:\/\/"
  verify text "click here to change something" not present
  verify text "Click Here To Change Url" present

INPUT RANGE
  set element #range value as 11
  verify element #range value is 11

CHECKBOX

SELECT TAG
  select "C" from #select
  verify element "#select option:nth-child(3)" selected

TEXTAREA
  click textarea
  clear textarea
  verify element textarea value is ""

DRAG-AND-DROP
  mouse down on element "#canvas"
  sleep 600 milliseconds
  move mouse x 100 y 10
  sleep 100 milliseconds
  move mouse x 0 y 20
  sleep 100 milliseconds
  move mouse x 0 y 30
  sleep 100 milliseconds
  mouse up
  sleep 1000 milliseconds

SUBMIT
  click #enable
  submit

SAVE VARIABLE
  run script "var today=new Date(); return today" and save to "today"
  print variable "today"
  verify script "today.getYear() === (new Date()).getYear()"

  enter text 'We can save variable' into "input#my-input"
  save value of element "input#my-input" to 'myInputVariable'
  print variable "myInputVariable"
  verify script "myInputVariable === 'We can save variable'"

ALERT/CONFIRM
  run script "alert(12345)"
  verify alert "12345" and click ok
  run script "confirm(12345)"
  verify alert "12345" and click cancel

PRESENCE
  verify element #presence present
  verify element #presence-not not present

CLASS NAMES
  verify element #class-names with class test1
  verify element #class-names with class test2
  verify element #class-names without class test3

END
  wait for page load
  close browser
