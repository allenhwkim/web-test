This is to test all core commands with test-page.html
  open browser
  go to http://localhost:8000/test/test-page.html
  set window position 0 0
  set window size 100 600
  see "click here to change background color"

#click test
  click
  click "body div#change-color"

#click link test
  click link "Click Here To Change Url"

#enter text into test
  enter text 'Hello World' into text
  press "A" into text
  set speed 1 second
  verify element #submit is disabled

#set value test
  set element input#range value 50
  verify element input#range value is "50"

#radio button test
  click #radio1
  verify element #radio2 is not selected
  verify element #hidden is not visible

#hidden element test
  click #show-hidden
  verify element #hidden is visible
  click #change-color

#verify commands test
  verify element #change-color style background-color is "rgba(255, 255, 0, 1)"
  verify element #hidden text matches "^Hidden\ Element"
  verify title is "Test Form"
  verify title matches "Form"
  verify url matches "^http:\/\/"
  verify text "click here to change something" not present
  verify text "Click Here To Change Url" present

#textarea test
  click textarea
  clear textarea
  verify element textarea value is ""

#submit button test
  set speed 1 millisecond
  click #enable
  verify element #submit is enabled
  submit
  wait for page load

#drag and drop test
  drag element 'drag1' 10 pixel up, then drop

#variable test
  save value of expression "var today=new Date(); return today" to "today"
  print variable "today"
  verify expression "today.getYear() === (new Date()).getYear()"

  save value of element "input#my-input" to 'myInputVariable'
  print variable "myInputVariable"
  verify expression "myInputVariable === 'hello world'"

#end of test
  close browser
